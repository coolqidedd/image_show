buildscript {
    ext {
        springBootVersion = "1.5.9.RELEASE"
    }
    repositories {
        jcenter()
        mavenCentral()
        maven { url 'http://repo.spring.io/plugins-release' }
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
        classpath 'io.spring.gradle:propdeps-plugin:0.0.9.RELEASE'
    }
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'

configurations {
    all*.exclude module: 'spring-boot-starter-logging'
    all*.exclude module: "logback-classic"
}

ext {
    if (project.hasProperty('profile')) {
        profile = project['profile']
    } else {
        profile = "dev"
    }
    println "profile:" + profile
}

group = 'studio.istart'
version = '1.0.0_' + profile

description = """tiles project"""


repositories {
    maven { url "https://repo.spring.io/libs-release" }
    maven { url "http://repo.maven.apache.org/maven2" }
    maven { url "https://artifacts.unidata.ucar.edu/content/repositories/unidata-releases/" }
}

dependencies {
    compile group: 'com.google.guava', name: 'guava', version: '23.2-jre'

    compile group: 'com.google.code.gson', name: 'gson', version: '2.8.2'


    //spring boot framework
    compile group: 'org.springframework.boot', name: 'spring-boot-starter', version: springBootVersion
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-log4j2', version: springBootVersion
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-web', version: springBootVersion
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-actuator', version: springBootVersion
//    compile group: 'org.springframework.boot', name: 'spring-boot-starter-thymeleaf', version: springBootVersion

    //the IDE plugin "lombok" should be install,java9 with 1.16.19+ will be fine.
    compileOnly group: 'org.projectlombok', name: 'lombok', version: '1.16.18'
    //test
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile(group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: springBootVersion) {
        exclude(module: 'commons-logging')
    }
    testCompile group: 'com.jayway.jsonpath', name: 'json-path', version: '2.2.0'
    //doc
    compile group: 'io.springfox', name: 'springfox-swagger2', version: '2.7.0'
    compile group: 'com.github.xiaoymin', name: 'swagger-bootstrap-ui', version: '1.6'
}

compileJava {
    options.encoding = "UTF-8"
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}
sourceSets {
    main {
        resources {
            srcDir "src/main/resources_${profile}"
        }
    }
}

jar {
    manifest {
        attributes 'Main-Class': 'com.inuorui.checkin.Application'
    }
}
build {
    println("=====================build." + version + "=========================")
    doLast {
        copy {
            from("${buildDir}/libs") {
                include '*.jar'
            }
            into "${buildDir}/release/app/"
            rename '(.*)-.*.jar', '$1.jar'
        }
        copy {
            from("./script/ready.sh")
            into "${buildDir}/release/"
        }
        println("=====================build.release success.=========================")
    }
}